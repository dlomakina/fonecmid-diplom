#Область ПрограммныйИнтерфейс

//Функция получает на вход текст сообщения
//
//Параметры:
//СообщениеТекст - Строка
//
//Возвращаемое значение:
//Булево 
//Успешность получения ответа
Функция ОтправитьСообщение(СообщениеТекст) Экспорт
	
	Токен = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();
	КодГруппы = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	
	СтруктураТелаЗапроса = Новый Структура;
	
	СтруктураТелаЗапроса.Вставить("chat_id", Число(КодГруппы));
	СтруктураТелаЗапроса.Вставить("text", СообщениеТекст);
	
	ЗаписьJSON = Новый ЗаписьJSON;      
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON,СтруктураТелаЗапроса);      
	ТелоЗапроса = ЗаписьJSON.Закрыть();
  
	АдресСервера = "api.telegram.org";
	БазовыйАдрес = СтрШаблон("bot%1", Токен);
	Метод = "/sendMessage";
		
	Ответ = HTTPSЗапросPOST(АдресСервера, БазовыйАдрес, Метод, Токен, ТелоЗапроса);
	
	Если НЕ Ответ.КодСостояния = 200 Тогда
		ТелоОтвета = Ответ.ПолучитьТелоКакСтроку("UTF-8");
		СоздатьЗаписьОбОшибкеВЖурналеРегистрации(ТелоОтвета);
		Результат = Ложь;
	Иначе
		Результат = Истина;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// 
Процедура ОтправкаНакопленныхСообщений() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВКМ_УведомленияТелеграмБоту.Ссылка,
		|	ВКМ_УведомленияТелеграмБоту.ВКМ_Текст КАК Текст
		|ИЗ
		|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Успех = ОтправитьСообщение(ВыборкаДетальныеЗаписи.Текст);
		
		Если Успех Тогда
			
			ЭлементОбъект = ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект();
			ЭлементОбъект.Удалить();
			
		КонецЕсли;	

	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
//Функция отправляет http-запрос POST
//
//Параметры:
//АдресСервера - Строка
//БазовыйАдрес - Строка
//Метод - Строка
//Тело - Строка
//
//Пример:
//АдресСервера = "api.telegram.org";
//БазовыйАдрес = "bot[ВашТокен]";
//Метод = "/sendMessage";
//
//Возвращаемое значение:
//HTTP-ответ

Функция HTTPSЗапросPOST(АдресСервера, БазовыйАдрес, Метод, Токен, Тело)
	
	БезопасноеСоединение = Новый HTTPСоединение(АдресСервера, 443,,,,5,Новый ЗащищенноеСоединениеOpenSSL());
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-Type", "application/json; charset=UTF-8");

	Запрос = Новый HTTPЗапрос(БазовыйАдрес + Метод, Заголовки);
	Запрос.УстановитьТелоИзСтроки(Тело,
								  КодировкаТекста.UTF8,
								  ИспользованиеByteOrderMark.НеИспользовать);
	Ответ = БезопасноеСоединение.ОтправитьДляОбработки(Запрос);
	
	Возврат Ответ;
	
КонецФункции

//Процедура формирует запись об ошибке в журнале регистрации

Процедура СоздатьЗаписьОбОшибкеВЖурналеРегистрации(Ошибка)
	
	СтруктураСобытий = Новый Структура("ИмяСобытия, ПредставлениеУровня, Комментарий, ДатаСобытия");
	
	СтруктураСобытий.ИмяСобытия = "Ошибка";
	СтруктураСобытий.ПредставлениеУровня = "Информация";	
	СтруктураСобытий.Комментарий = "Комментарий к событию";
	СтруктураСобытий.ДатаСобытия = ТекущаяДатаСеанса();
	
	СобытияДляЖурналаРегистрации = Новый СписокЗначений();
	СобытияДляЖурналаРегистрации.Добавить(СтруктураСобытий);
	
	ЖурналРегистрации.ЗаписатьСобытияВЖурналРегистрации(СобытияДляЖурналаРегистрации);	

КонецПроцедуры

#КонецОбласти