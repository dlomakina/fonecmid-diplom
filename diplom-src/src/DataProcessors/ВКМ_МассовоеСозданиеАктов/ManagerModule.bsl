#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Функция СозданиеСпискаНаСервере(Дата, СоздатьОбъект) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Владелец КАК Контрагент,
	|	ДоговорыКонтрагентов.Ссылка,
	|	ДоговорыКонтрагентов.Организация
	|ПОМЕСТИТЬ ВТ_ДанныеДоговора
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	&Дата МЕЖДУ ДоговорыКонтрагентов.ВКМ_ДатаНачалаДействия И ДоговорыКонтрагентов.ВКМ_ДатаОкончанияДействия
	|	И ДоговорыКонтрагентов.ВидДоговора = ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКонтрагентов.ВКМ_АбонентскоеОбслуживание)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияТоваровУслуг.Ссылка,
	|	РеализацияТоваровУслуг.Контрагент,
	|	РеализацияТоваровУслуг.Договор
	|ПОМЕСТИТЬ ВТ_ДанныеРеализации
	|ИЗ
	|	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
	|ГДЕ
	|	РеализацияТоваровУслуг.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И РеализацияТоваровУслуг.ПометкаУдаления = ЛОЖЬ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ДанныеДоговора.Ссылка КАК Договор,
	|	ВТ_ДанныеРеализации.Ссылка КАК Реализация,
	|	ВТ_ДанныеДоговора.Организация,
	|	ВТ_ДанныеДоговора.Контрагент
	|ИЗ
	|	ВТ_ДанныеДоговора КАК ВТ_ДанныеДоговора
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ДанныеРеализации КАК ВТ_ДанныеРеализации
	|		ПО ВТ_ДанныеДоговора.Ссылка = ВТ_ДанныеРеализации.Договор";

	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("ДатаОкончания", КонецМесяца(Дата));

	Результат = Запрос.Выполнить().Выбрать();

	МассивРеализаций = Новый Массив;

	Пока Результат.Следующий() Цикл
		РеализацииСтруктура = Новый Структура;
		Если СоздатьОбъект Тогда
			Если НЕ ЗначениеЗаполнено(Результат.Реализация) Тогда
				НоваяРеализация = СозданиеРеализации(Результат, КонецМесяца(Дата));
				РеализацииСтруктура.Вставить("Договор", Результат.Договор);
				РеализацииСтруктура.Вставить("Реализация", НоваяРеализация);
			Иначе
				РеализацииСтруктура.Вставить("Договор", Результат.Договор);
				РеализацииСтруктура.Вставить("Реализация", Результат.Реализация);
			КонецЕсли;
		Иначе
			РеализацииСтруктура.Вставить("Договор", Результат.Договор);
			РеализацииСтруктура.Вставить("Реализация", Результат.Реализация);
		КонецЕсли;

		МассивРеализаций.Добавить(РеализацииСтруктура);

	КонецЦикла;

	Возврат МассивРеализаций;

КонецФункции

Функция СозданиеРеализации(Результат, Дата)

	НовыйДок = Документы.РеализацияТоваровУслуг.СоздатьДокумент();
	НовыйДок.Дата = Дата;
	НовыйДок.Договор = Результат.Договор;
	НовыйДок.Контрагент = Результат.Контрагент;
	НовыйДок.Организация = Результат.Организация;
	НовыйДок.ВыполнитьАвтозаполнение();
	НовыйДок.Ответственный = Пользователи.ТекущийПользователь();
	
	Если НовыйДок.ПроверитьЗаполнение() Тогда

		Попытка  
			НачатьТранзакцию();
			НовыйДок.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ОбщегоНазначения.СообщитьПользователю("Невозможно провести документ!");
			ЗаписьЖурналаРегистрации("Отмена массового создания актов.");
		КонецПопытки;

	Иначе
		ОбщегоНазначения.СообщитьПользователю(СтрШаблон(
			"Не удалось создать документ реализации по Договору: %1. 
			|Проверьте заполнение обязательных полей", Результат.Договор));
	КонецЕсли;

	Возврат НовыйДок.Ссылка;

КонецФункции

#КонецОбласти

#КонецЕсли