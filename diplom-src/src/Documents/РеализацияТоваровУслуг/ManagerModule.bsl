#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДобавитьКомандуСоздатьНаОсновании(КомандыСозданияНаОсновании) Экспорт

	Если ПравоДоступа("Добавление", Метаданные.Документы.РеализацияТоваровУслуг) Тогда

		КомандаСоздатьНаОсновании = КомандыСозданияНаОсновании.Добавить();
		КомандаСоздатьНаОсновании.Менеджер = Метаданные.Документы.РеализацияТоваровУслуг.ПолноеИмя();
		КомандаСоздатьНаОсновании.Представление = ОбщегоНазначения.ПредставлениеОбъекта(
			Метаданные.Документы.РеализацияТоваровУслуг);
		КомандаСоздатьНаОсновании.РежимЗаписи = "Проводить";

		Возврат КомандаСоздатьНаОсновании;

	КонецЕсли;

	Возврат Неопределено;

КонецФункции

// Ломакина <<Команда печати акта об оказании услуг

Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт оказанных услуг
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "АктОбОказанииУслуг";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказании услуг'");
	КомандаПечати.Порядок = 5;

КонецПроцедуры

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт

	ПечатнаяФорма = УправлениеПечатью.СведенияОПечатнойФорме(КоллекцияПечатныхФорм, "АктОбОказанииУслуг");
	Если ПечатнаяФорма <> Неопределено Тогда
		ПечатнаяФорма.ТабличныйДокумент = ПечатьАкта(МассивОбъектов, ОбъектыПечати);
		ПечатнаяФорма.СинонимМакета = НСтр("ru = 'Акт об оказании услуг'");
		ПечатнаяФорма.ПолныйПутьКМакету = "Документ.РеализацияТоваровУслуг.ПФ_MXL_ВКМ_АктОбОказанииУслуг";
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьАкта(МассивОбъектов, ОбъектыПечати)

	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Акт";

	Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.РеализацияТоваровУслуг.ПФ_MXL_ВКМ_АктОбОказанииУслуг");

	ДанныеДокументов = ПолучитьДанныеДокументов(МассивОбъектов);

	ПервыйДокумент = Истина;

	Пока ДанныеДокументов.Следующий() Цикл

		Если Не ПервыйДокумент Тогда
			// Все документы нужно выводить на разных страницах.
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;

		ПервыйДокумент = Ложь;

		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;

		ВывестиЗаголовокАкта(ДанныеДокументов, ТабличныйДокумент, Макет);

		ВывестиТелоТаблицыАкта(ДанныеДокументов, ТабличныйДокумент, Макет);

		ВывестиПодвалАкта(ДанныеДокументов, ТабличныйДокумент, Макет);
		
        // В табличном документе необходимо задать имя области, в которую был 
        // выведен объект. Нужно для возможности печати комплектов документов.
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати,
			ДанныеДокументов.Ссылка);

	КонецЦикла;

	Возврат ТабличныйДокумент;

КонецФункции

Функция ПолучитьДанныеДокументов(МассивОбъектов)

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	РеализацияТоваровУслуг.Дата,
				   |	РеализацияТоваровУслуг.Номер,
				   |	РеализацияТоваровУслуг.Ссылка,
				   |	РеализацияТоваровУслуг.Организация,
				   |	РеализацияТоваровУслуг.Контрагент,
				   |	РеализацияТоваровУслуг.Договор,
				   |	РеализацияТоваровУслуг.СуммаДокумента,
				   |	РеализацияТоваровУслуг.Основание,
				   |	РеализацияТоваровУслуг.Ответственный,
				   |	РеализацияТоваровУслуг.Комментарий,
				   |	РеализацияТоваровУслуг.Услуги.(
				   |		Ссылка,
				   |		НомерСтроки,
				   |		Номенклатура,
				   |		Количество,
				   |		Цена,
				   |		Сумма)
				   |ИЗ
				   |	Документ.РеализацияТоваровУслуг КАК РеализацияТоваровУслуг
				   |ГДЕ
				   |	РеализацияТоваровУслуг.Ссылка В (&МассивДокументов)";

	Запрос.УстановитьПараметр("МассивДокументов", МассивОбъектов);

	Возврат Запрос.Выполнить().Выбрать();

КонецФункции

Процедура ВывестиЗаголовокАкта(ДанныеДокументов, ТабличныйДокумент, Макет)

	ОбластьЗаголовокАкта = Макет.ПолучитьОбласть("Шапка");
	ОбластьЗаголовокТаблицы = Макет.ПолучитьОбласть("ШапкаТаблицы");

	ДанныеПечати = Новый Структура;

	ТекстЗаголовка = СтрШаблон("Акт об оказании услуг №%1 от %2", 
								ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ДанныеДокументов.Номер),
								Формат(ДанныеДокументов.Дата, "ДЛФ=DD"));
	ДанныеПечати.Вставить("ТекстЗаголовка", ТекстЗаголовка);

	ДанныеПечати.Вставить("Исполнитель", ДанныеДокументов.Организация);
	ДанныеПечати.Вставить("Заказчик", ДанныеДокументов.Контрагент);
	ДанныеПечати.Вставить("Договор", ДанныеДокументов.Договор);

	ОбластьЗаголовокАкта.Параметры.Заполнить(ДанныеПечати);

	ДанныеПечати = Новый Структура;
	
	ОбластьЗаголовокТаблицы.Параметры.Заполнить(ДанныеПечати);

	ТабличныйДокумент.Вывести(ОбластьЗаголовокАкта);

	ТабличныйДокумент.Вывести(ОбластьЗаголовокТаблицы);

КонецПроцедуры

Процедура ВывестиТелоТаблицыАкта(ДанныеДокументов, ТабличныйДокумент, Макет)

	ОбластьТело = Макет.ПолучитьОбласть("ТелоТаблицы");

	Выборка = ДанныеДокументов.Услуги.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ОбластьТело.Параметры.Заполнить(Выборка);
		ТабличныйДокумент.Вывести(ОбластьТело);
		
	КонецЦикла;

КонецПроцедуры

Процедура ВывестиПодвалАкта(ДанныеДокументов, ТабличныйДокумент, Макет)

	ОбластьПодвал = Макет.ПолучитьОбласть("Подвал");

	ДанныеПечати = Новый Структура;
	ДанныеПечати.Вставить("Итого", ДанныеДокументов.СуммаДокумента);

	СуммаПрописью = ЧислоПрописью(ДанныеДокументов.СуммаДокумента, "Л=ru_RU;ДП=Истина",
							"рубль, рубля, рублей, м, копейка, копейки, копеек, ж, 2"); 
	
	ИтоговаяПодпись = СтрШаблон("Всего оказано услуг на общую сумму: %1 руб.%2(%3)", 
							Формат(ДанныеДокументов.СуммаДокумента, "ЧДЦ=2;"),
							Символы.ПС, 
							СуммаПрописью);
	ДанныеПечати.Вставить("ОказаноУслуг", ИтоговаяПодпись);

	ОбластьПодвал.Параметры.Заполнить(ДанныеПечати);

	ТабличныйДокумент.Вывести(ОбластьПодвал);

КонецПроцедуры

#КонецОбласти

// Ломакина >>

#КонецЕсли